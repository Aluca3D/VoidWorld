plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "$runPaperVersion"
    id("com.gradleup.shadow") version "$shadowVersion"
}

group = 'io.papermc'
version = "$projectVersion"

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:$paperVersion")

    implementation("org.spongepowered:configurate-core:$configurateVersion")
    implementation("org.spongepowered:configurate-jackson:$configurateVersion")
}

tasks.register('updatePaperPluginYaml') {
    doLast {
        def paperPluginFile = file('src/main/resources/paper-plugin.yml')
        def buildPaperPluginFile = file(layout.buildDirectory.dir("resources/paper-plugin.yml"))

        if (paperPluginFile.exists()) {
            def props = new Properties()
            props.load(new FileInputStream('gradle.properties'))
            def projectVersion = props.getProperty('projectVersion')
            def apiVersion = props.getProperty('apiVersion')

            def yamlContent = paperPluginFile.text
            yamlContent = yamlContent.replace('$projectVersion', projectVersion)
            yamlContent = yamlContent.replace('$apiVersion', apiVersion)
            buildPaperPluginFile.text = yamlContent
        }
    }
}

tasks {
    runServer {
        minecraftVersion("$gameVersion")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    dependsOn "updatePaperPluginYaml"

    from('src/main/resources') {
        include 'paper-plugin.yml'
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    def props = [
            version       : version,
            projectVersion: project.findProperty("projectVersion"),
            apiVersion    : project.findProperty("apiVersion")
    ]
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.shadowJar {
    archiveClassifier.set("")
    relocate "org.spongepowered.configurate", "io.papermc.voidworld.lib.configurate"
}

tasks.named("build") {
    dependsOn(tasks.named("shadowJar"))
}